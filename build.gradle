plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'

    id 'org.cadixdev.licenser' version '0.6.1'
}

group = 'cn.maxpixel.gradle'
version = '0.10.0-SNAPSHOT'
description = 'A Gradle plugin to manage patches for Git repositories'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

dependencies {
    implementation gradleApi()
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    plugins {
        gitpatcher {
            id = 'cn.maxpixel.gitpatcher'
            implementationClass = 'org.cadixdev.gradle.gitpatcher.GitPatcher'
        }
    }
}

pluginBundle {
    website = 'https://github.com/MaxPixelStudios/gitpatcher'
    vcsUrl = website
    description = project.description
    tags = ['git']

    plugins {
        gitpatcher {
            displayName = 'gitpatcher'
        }
    }
}

publishing {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/MaxPixelStudios/gitpatcher'
            credentials {
                username = System.getenv('GPR_USERNAME')
                password = System.getenv('GPR_TOKEN')
            }
        }
    }
}